System arch1


/* Messaggi per l'interazione con cargoservice */
Request load_product  : load_product(PID) 			    	 "la richiesta di carico di un container che arriva a cargoservice: contiene il PID del prodotto"
Reply   load_accepted : load_accepted(SLOT) for load_product "può essere accettata, con restituzione dello slot assegnato al container"
Reply   load_refused  : load_refused(CAUSA) for load_product "può essere rifiutata per vari motivi (peso, slot occupati, PID inesistente)"

/* Messaggi per l'interazione con productservice */
Request get_weight    	   : get_weight(PID) 			    		   "query verso productservice per ottenere il peso di un prodotto. Contiene il PID del prodotto"
Reply   get_weight_success : get_weight_success(WEIGHT) for get_weight "ha successo se il PID è presente nel DB"
Reply   get_weight_fail    : get_weight_fail(CAUSA)     for get_weight "fallisce se il PID è inesistente"

/* Messaggi per l'interazione con hold*/
Request reserve_slot         : reserve_slot(WEIGHT) 			    	   "richiesta verso hold per prenotare uno slot. Contiene il peso del prodotto da caricare"
Reply   reserve_slot_success : reserve_slot_success(SLOT) for reserve_slot "se la richiesta è soddisfacibile, hold restituisce il nome/id dello slot prenotato"
Reply   reserve_slot_fail    : reserve_slot_fail(CAUSA) for reserve_slot   "fallisce se il peso supera MaxLoad oppure se non c'è uno slot libero"

/* Messaggi per l'interazione con cargorobot */
Request handle_load_operation   : handle_load_operation(SLOT) 			  				"richiesta verso cargorobot per l'intervento di carico"
Reply   load_operation_complete : load_operation_complete(OK) for handle_load_operation "l'intervento di carico non può fallire quindi prevediamo solo una risposta"

/* Eventi del sonar */
Event container_arrived : container_arrived(X) "evento che avvisa della presenza di un container presso l'IO-port"
Event container_absent  : container_absent(X)  "evento che avvisa dell'assenza di un container presso l'IO-port"
Event interrompi_tutto  : interrompi_tutto(X)  "evento che avvisa di un malfunzionamento del sonar e che quindi costringe a interrompre tutte le attività"
Event riprendi_tutto    : riprendi_tutto(X)    "evento che avvisa del ripristino del sonar e che quindi permette di continuare tutte le attività"

/* Messaggio che uso per autodispatch */
Dispatch continue : continue(X)    


/* Messaggi per l'interazione con il basicrobot */
Dispatch cmd       	: cmd(MOVE)         

Dispatch end       	: end(ARG)         
 
Request step       : step(TIME)	
Reply stepdone     : stepdone(V)                 for step
Reply stepfailed   : stepfailed(DURATION, CAUSE) for step

Event  sonardata   : sonar( DISTANCE ) 	   
Event obstacle     : obstacle(X) 
Event info         : info(X)    

Request  doplan     : doplan( PATH, STEPTIME )
Reply doplandone    : doplandone( ARG )    for doplan
Reply doplanfailed  : doplanfailed( ARG )  for doplan

Dispatch setrobotstate: setpos(X,Y,D) //D =up|down!left|right

Request engage        : engage(OWNER, STEPTIME)	
Reply   engagedone    : engagedone(ARG)    for engage
Reply   engagerefused : engagerefused(ARG) for engage

Dispatch disengage    : disengage(ARG)

Request checkowner    : checkowner(CALLER)
Reply checkownerok    : checkownerok(ARG)      for checkowner
Reply checkownerfailed: checkownerfailed(ARG)  for checkowner
  
Event alarm           : alarm(X)
Dispatch nextmove     : nextmove(M)
Dispatch nomoremove   : nomoremove(M)
 
Dispatch setdirection : dir( D )  //D =up|down!left|right

Request moverobot    :  moverobot(TARGETX, TARGETY)  
Reply moverobotdone  :  moverobotok(ARG)                    for moverobot
Reply moverobotfailed:  moverobotfailed(PLANDONE, PLANTODO) for moverobot
 
Request getrobotstate : getrobotstate(ARG)
Reply robotstate      : robotstate(POS,DIR)  for getrobotstate

Request getenvmap     : getenvmap(X)
Reply   envmap        : envmap(MAP)  for getenvmap






Context ctx_cargoservice 			ip [host="localhost" port=8000]
//Context ctx_cargoservice_clients 	ip [host="localhost" port=9000]
//Context ctx_iodevices 				ip [host="localhost" port=8001]
Context ctxbasicrobot ip [host="127.0.0.1"  port=8020   ]  

ExternalQActor basicrobot context ctxbasicrobot   




/*********** CARGOSERVICE ***********/

QActor cargoservice context ctx_cargoservice {
	[#
		var Cur_prod_PID = -1
		var Cur_prod_weight = -1
	#] 
	
	State s0 initial{
		println("$name | STARTS") color blue
		
		[#
			// inizializzo lo stato 
		#]
	} 
	Goto wait_request
	
	
	
	/* inizio ciclo di servizio */
	
	State wait_request{
		println("$name | waiting for request") color blue
	}
	Transition t0
	   whenRequest load_product -> get_prod_weight
	




	/* Tento di recuperare il peso del prodotto richiesto */
	
	State get_prod_weight {
		onMsg( load_product : load_product(PID) ) {
			[# Cur_prod_PID = payloadArg(0).toInt() #]
			println("$name | checking with productservice for the weight of PID: $Cur_prod_PID") color blue
			
			request productservice -m get_weight : get_weight($Cur_prod_PID)
		}
	}
	Transition t0
	   whenReply get_weight_success -> reserve_slot
	   whenReply get_weight_fail	-> get_weight_fail
	
	
	State get_weight_fail {
		onMsg( get_weight_fail : get_weight_fail(CAUSA) ) {
			[# 
				val Causa = payloadArg(0) 
				val CausaMsg = "Non è stato possibile recuperare il peso di PID: $Cur_prod_PID da productservice. \n\tCausa: $Causa"
			#]
			println("$name | $CausaMsg") color red
			
			replyTo load_product with load_refused : load_refused($CausaMsg)
		}
	}
    Goto wait_request
	
	
	
	
	
	/* Tento di prenotare uno slot */
	
	State reserve_slot {
		onMsg( get_weight_success : get_weight_success(PESO) ) {
			[# 
				Cur_prod_weight = payloadArg(0).toInt() 
			#]
			
			println("$name | checking with hold if a reservation with (PID: $Cur_prod_PID, KG: $Cur_prod_weight) is possible") color blue
			
			request hold -m reserve_slot : reserve_slot($Cur_prod_weight)
		}
	}
    Transition t0
	   whenReply reserve_slot_success -> load_cargo
	   whenReply reserve_slot_fail	  -> reserve_slot_fail
    
    State reserve_slot_fail {
		onMsg( reserve_slot_fail : reserve_slot_fail(CAUSA) ) {
			[# 
				val Causa = payloadArg(0) 
				val CausaMsg = "Non è stato possibile prenotare uno slot per (PID: $Cur_prod_PID, KG: $Cur_prod_weight). \n\tCausa: $Causa"
			#]
			println("$name | $CausaMsg") color red
			
			replyTo load_product with load_refused : load_refused($CausaMsg)
		}
	}
    Goto wait_request
	
	
	
	
	
	/* Richiesta soddisfacibile */
	
	State load_cargo {
		onMsg( reserve_slot_success : reserve_slot_success(SLOT) ) {
			[# 
				val Reserved_slot = payloadArg(0)
			#]
			
			println("$name | product (PID: $Cur_prod_PID, KG: $Cur_prod_weight) is going to be placed in slot: $Reserved_slot") color green
			replyTo load_product with load_accepted : load_accepted($Reserved_slot)

			println("$name | waiting for load completion") color blue
			request cargorobot -m handle_load_operation : handle_load_operation($Reserved_slot)
		}
	}
    Transition t0
	   whenReply load_operation_complete -> load_request_done
	 
	State load_request_done {
		println("$name | product (PID: $Cur_prod_PID, KG: $Cur_prod_weight) successfully loaded!") color green
	}
	Goto wait_request
}










/*********** CARGOSERVICE ***********/








QActor cargorobot context ctx_cargoservice{
	[#
		// stato
		val positions = hashMapOf(
			"home"    	to arrayOf(0, 0),
			"io_port" 	to arrayOf(0, 4),
		    "slot1"   	to arrayOf(1, 1),
		    "slot2" 	to arrayOf(1, 3),
		    "slot3" 	to arrayOf(4, 1),
		    "slot4" 	to arrayOf(4, 3)
		)

		lateinit var Destination : String
		lateinit var Reserved_slot : String
		
		var moving 	= false
		
		var container_present = false
	#]
    
	State s0 initial {
		println("$name | STARTS") color magenta
		
		// TODO: leggi config
				
		println("$name | $MyName engaging ... ") color yellow 
		request basicrobot -m engage : engage($MyName, 330) // durata del passo configurabile?
	}
	Transition t0
		whenReply engagedone    -> wait_request  
 	  	whenReply engagerefused -> end
	
	  
	  
	  
	/* inizio ciclo di servizio */
	
	State wait_request{
		println("$name | waiting for request") color magenta
		[# moving = false #]
	}
	Transition t0
		whenInterruptEvent interrompi_tutto  -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
		whenRequest handle_load_operation    -> go_to_io_port
	   
	  
//	 TODO: se arriva un interrupt qua viene perso, meglio spostare questa roba dentro
//	State get_reserved_slot {
//		// giusto per essere espliciti
//		[# moving = false #]
//		
//		onMsg( handle_load_operation : handle_load_operation(SLOT) ) {
//			[# 
//				Reserved_slot = payloadArg(0)
//								
//				// il doppio !! serve a dire al compilatore Kotlin di stare tranquillo 
//				// e di recuperare il valore dalla mappa anche senza fare dei null-check
//				val coords = positions[Reserved_slot]!!
//				val X = coords[0]
//				val Y = coords[1]
//			#]
//			// DEBUG: 
//			println("$name | cargorobot reserved_slot is $Reserved_slot = ($X, $Y)") color magenta
//		}
//	}
//    Goto go_to_io_port
	
	
		
		
		
	/* vado a prendere il container */
	
	State go_to_io_port {
		// aggiorno il mio stato recuperando le coordinato dello slot prenotato
		onMsg( handle_load_operation : handle_load_operation(SLOT) ) {
			[# 
				Reserved_slot = payloadArg(0)
								
				// il doppio !! serve a dire al compilatore Kotlin di stare tranquillo 
				// e di recuperare il valore dalla mappa anche senza fare dei null-check
				val coords = positions[Reserved_slot]!!
				val X = coords[0]
				val Y = coords[1]
			#]
			// DEBUG: 
			println("$name | cargorobot reserved_slot is $Reserved_slot = ($X, $Y)") color magenta
		}
		
		// vado verso la io-port
		println("$name | going to io-port") color magenta
		[#
			// aggiorno la mia destinazione per ricordarmi dove devo andare in caso di interruzioni
			Destination = "io_port"
			
			val coords = positions[Destination]!!
			val X = coords[0]
			val Y = coords[1]
		#]
		
    	request basicrobot -m  moverobot : moverobot($X, $Y)
    	
		[# moving = true #]
 	}  	
  	Transition t0 
  		whenInterruptEvent interrompi_tutto  -> wait_resume_msg
  		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
  		whenReply moverobotdone 		     -> arrived_at_io_port
		

	
	State arrived_at_io_port {
		println("$name | arrived at io-port") color magenta
		[# moving = false #]
		
		// se il container c'è gia mi mando un autodispatch cosi da non dover aspettare
		if [# container_present #] {
			[# container_present = false #]
			autodispatch continue : continue(si)
		}
 	}  	
 	Transition t0
 		whenInterruptEvent interrompi_tutto -> wait_resume_msg
 		whenMsg continue 					-> pick_up_container
 		whenEvent container_arrived 		-> pick_up_container


	State pick_up_container {
		println("$name | picking up container") color magenta
		[# moving = false #] // giusto per essere espliciti
		
		delay 3000 // tempo arbitrario per caricare il container sul cargorobot
		// durante il carico del container potrebbe essere arrivato una interruzione,
		// mi mando un messaggio per ricordarmi che posso procedere
		autodispatch continue : continue(si)
	}
	Transition t0
		whenInterruptEvent interrompi_tutto -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
		whenMsg continue 					-> go_to_reserved_slot 
		



	/* vado a depositare il container */
	
	State go_to_reserved_slot {
		[#
			// aggiorno la mia destinazione per ricordarmi dove devo andare in caso di interruzioni
			Destination = Reserved_slot
			
			val coords = positions[Destination]!!
			val X = coords[0]
			val Y = coords[1]
		#]
		
		println("$name | going to my reserved slot: $Reserved_slot = ($X, $Y)") color magenta
		
    	request basicrobot -m  moverobot : moverobot($X, $Y)
    	[# moving = true #]
 	}  	
 	Transition t0 
 		whenInterruptEvent interrompi_tutto  -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
 		whenReply moverobotdone   			 -> arrived_at_reserved_slot
 		
		
	State arrived_at_reserved_slot {
		println("$name | arrived at reserved slot") color magenta
		[# moving = false #]
		
		println("$name | laying down the container") color magenta
		delay 3000 // tempo arbitrario per scaricare il container dal cargorobot
		 
		// duranto lo scarico potrebbe essere arrivato una interruzione,
		// mi mando un messaggio per ricordarmi che posso procedere
		autodispatch continue : continue(si)
	}
	Transition t0
		whenInterruptEvent interrompi_tutto  -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
		whenMsg continue 					 -> back_to_home 
 	
   	
   	
   	
   	
   	/* torno a casa */
   	
  	State back_to_home {
  		println("$name | load operation completed") color magenta
		replyTo handle_load_operation with load_operation_complete : load_operation_complete(ok)
		
		
		println("$name | Back to home") color magenta
		[#
			// aggiorno la mia destinazione per ricordarmi dove devo andare in caso di interruzioni
			Destination = "home"
			
			val coords = positions[Destination]!!
			val X = coords[0]
			val Y = coords[1]
		#]

		request basicrobot -m  moverobot : moverobot($X, $Y)
		[# moving = true #]
	}  	
  	Transition t0 
  		whenInterruptEvent interrompi_tutto  -> wait_resume_msg
  		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
  		whenRequest handle_load_operation    -> stop_going_to_home
  		whenReply moverobotdone 			 -> atHome 
  		
  		
  	
  	
  	State stop_going_to_home {
   		println("$name | stop going to home and start serving new request immediately") color magenta
   		emit alarm : alarm(blocca) // blocco il basicrobot
   		
   		// aggiorno il mio slot prenotato (devo farlo qua dato che consumo
   		// la richiesta 'handle_load_operation' prima di arrivare a 'go_to_io_port'
   		onMsg( handle_load_operation : handle_load_operation(SLOT) ) { 
			[# 
				Reserved_slot = payloadArg(0)
								
				// il doppio !! serve a dire al compilatore Kotlin di stare tranquillo 
				// e di recuperare il valore dalla mappa anche senza fare dei null-check
				val coords = positions[Reserved_slot]!!
				val X = coords[0]
				val Y = coords[1]
			#]
			// DEBUG: 
			println("$name | cargorobot reserved_slot is $Reserved_slot = ($X, $Y)") color magenta
		}
   		
   		// anche se estremamente improbabile, anche durante questo mini-stato 
   		// potrebbe essere arrivata una interruzione.
		// Mi mando un messaggio per ricordarmi che posso procedere
		autodispatch continue : continue(si)
   	}
   	Transition t0 
	   	whenInterruptEvent interrompi_tutto  -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
		whenMsg continue					 -> go_to_io_port
  		
  		
  		
  	State atHome{
   		println("$name | athome") color magenta
   		forward basicrobot -m setdirection : dir(down)
   		[# moving = false #]
   		
   		// anche se estremamente improbabile, anche durante questo mini-stato 
   		// potrebbe essere arrivata una interruzione.
		// Mi mando un messaggio per ricordarmi che posso procedere
		autodispatch continue : continue(si)
   	}
   	Transition t0 
	   	whenInterruptEvent interrompi_tutto  -> wait_resume_msg
		whenInterruptEvent container_arrived -> container_arrived_handler
		whenInterruptEvent container_absent  -> container_absent_handler
   		whenMsg continue 					 -> wait_request
   	
   	
   	
   	
   	/* gestisco le interruzioni */
   	
   	State wait_resume_msg {
   		println("$name | sonar malfunzionante, mi fermo") color red
   		
   		emit alarm : alarm(blocca) // blocco il basicrobot
	}
	Transition t0
		whenEvent riprendi_tutto -> resume
	
	
	State resume {
		println("$name | riprendo") color green
		
		// se il basic robot si stava muovendo, gli dico di nuovo dove deve andare
		// altrimenti rimane fermo dove è stato interrotto
		if [# moving #] {
			[#
				val coords = positions[Destination]!!
				val X = coords[0]
				val Y = coords[1]
			#]
	    	request basicrobot -m  moverobot : moverobot($X, $Y)
    	}
    	
    	returnFromInterrupt
	}
		
		
   	State container_arrived_handler {
   		println("$name | container arrivato") color yellow
   		
   		[# container_present = true #]
   		returnFromInterrupt
   	}
   	
	State container_absent_handler {
   		println("$name | container assente") color yellow
   		
   		[# container_present = false #]
   		returnFromInterrupt
   	}
   	
   	
   	
   	
   	
   	/* esco */
   	
   	State end{
		println("$name | ENDS ") color yellow
		[# System.exit(0) #]
	}	
} 
 




 
 
 
 
 
 
 
 
 
QActor productservice context ctx_cargoservice{
  	State s0 initial {
		println("$name | STARTS")
	} 
	Goto wait_request
	
	
	  
	State wait_request{
//		println("$name | waiting for request") color magenta
	}
	Transition t0
	   whenRequest get_weight -> handle_query
	  
	
	State handle_query{
//		println("$name | loading cargo") color blue
		
		delay 1000
		replyTo get_weight with get_weight_success : get_weight_success(10)
	}
	Goto wait_request
} 
 
	
QActor hold context ctx_cargoservice{
	[# 
		var Counter = 0
	#]
	
	State s0 initial {
		println("$name | STARTS")
	} 
	Goto wait_request
	
	  
	State wait_request{
//		println("$name | waiting for request") color magenta
	}
	Transition t0
	   whenRequest reserve_slot -> handle_request
	  
	
	State handle_request{
//		println("$name | loading cargo") color blue
		println("$name | slot counter $Counter") color yellow
		delay 1000
		
		if [# Counter == 0 #] {
			replyTo reserve_slot with reserve_slot_success : reserve_slot_success(slot4)
		} 
		
		if [# Counter == 1 #] {
			replyTo reserve_slot with reserve_slot_success : reserve_slot_success(slot3)
		}
		
		if [# Counter == 2 #] {
			replyTo reserve_slot with reserve_slot_success : reserve_slot_success(slot2)
		}
		
		if [# Counter == 3 #] {
			replyTo reserve_slot with reserve_slot_success : reserve_slot_success(slot1)
		}
		
		[# 
			Counter++ 
			
			if(Counter == 4)
				Counter = 0
		#]
	}
	Goto wait_request
} 







//QActor webgui context ctx_cargoservice{
// 	State s0 initial {
//  	} 
//}
//
//


QActor sonar context ctx_cargoservice{
	[#
		var counter = 0	
	#]
	
 	State s0 initial {
 		println("$name | STARTS") color yellow
  	} 
  	Goto work
  	
  	
  	
  	State work {
  		// ogni 10s arriva un container
  		delay 10000
  		emit container_arrived : container_arrived(si) 
  		[# counter ++ #]
 
  		
  		// ogni 3 container mi guasto
  		if [# counter%3 == 0 #] {
	  		delay 5000
//	    	println("$name | ALLARME") color red 	// DEBUG
	 		emit interrompi_tutto : interrompi_tutto(si) 
	 		
	 		delay 3000
//	 		println("$name | A POSTO") color green 	// DEBUG
	 		emit riprendi_tutto : riprendi_tutto(si) 
  		}
  	}
  	Goto work
}


//
//QActor led context ctx_cargoservice{
// 	State s0 initial {
//  	} 
//}
 

QActor external_client context ctx_cargoservice{
 	State s0 initial {
 		println("$name | STARTS") color cyan
  	} Goto work1
  	
  	State work1 {
 		println("$name | sends request") color cyan
 		request cargoservice -m load_product: load_product(10)
 		delay 2000
  	} 
  	Transition t0
	   whenReply load_accepted -> work2
	   
	   
	State work2 {
 		println("$name | sends request") color cyan
 		request cargoservice -m load_product: load_product(20)
 		delay 2000
  	} 
  	Transition t0
	   whenReply load_accepted -> work3
	   
	
	State work3 {
 		println("$name | sends request") color cyan
 		request cargoservice -m load_product: load_product(30)
 		delay 2000
  	} 
  	Transition t0
	   whenReply load_accepted -> work4
	   
	
	State work4 {
 		println("$name | sends request") color cyan
 		request cargoservice -m load_product: load_product(40)
 		delay 2000
  	} 
}
